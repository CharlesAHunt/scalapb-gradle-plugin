plugins {
    id 'scala'
    id 'idea'
//    id "com.github.alisiikh.scalastyle" version "3.1.0"
//    id 'cz.augi.gradle.wartremover' version '0.9.10'
    id 'cz.alenkacz.gradle.scalafmt' version '1.8.0'
    id "com.github.maiflai.scalatest" version "0.26"
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.10.1'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'maven-publish'
}

group 'com.charlesahunt'

version = '1.3.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile 'org.scala-lang:scala-library:2.12.8'
    compile 'org.scala-sbt:io_2.12:1.1.6'
    compile 'com.github.os72:protoc-jar:3.8.0'
    compile 'com.typesafe.scala-logging:scala-logging_2.12:3.9.2'
    compile 'com.google.protobuf:protobuf-java:3.10.0'
    compile 'com.thesamet.scalapb:compilerplugin_2.12:0.10.1'
    compile 'com.thesamet.scalapb:scalapb-runtime-grpc_2.12:0.10.1'
    compile 'com.thesamet.scalapb:scalapb-runtime_2.12:0.10.1'

    testCompile 'junit:junit:4.12'
    testCompile 'org.scalatest:scalatest_2.12:3.0.8'
    testRuntime 'org.pegdown:pegdown:1.4.2'
    testRuntime 'com.vladsch.flexmark:flexmark-all:0.35.10'

    testCompile gradleTestKit()
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

pluginBundle {
    website = 'https://github.com/CharlesAHunt/scalapb-gradle-plugin'
    vcsUrl = 'https://github.com/CharlesAHunt/scalapb-gradle-plugin'
    tags = ['scala', 'scalapb', 'proto', 'protobuf']

    plugins {
        scalapbPlugin {
            id = 'com.charlesahunt.scalapb'
            displayName = 'Gradle ScalaPB Plugin'
            description = 'This plugin uses the [ScalaPB](http://scalapb.github.io) compiler to generate Scala case' +
                    ' classes from protocol buffers and put them under the managed sources directory in your project.'
        }
    }
}

// Plugin publication
publishing {
    publications {
        pluginPublication (MavenPublication) {
            from        components.java
            groupId     project.group
            artifactId  project.name
            version     project.version
        }
    }
}


bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'com.charlesahunt.scalapb'
        name = 'gradle-scalapb'
        userOrg = 'com.charlesahunt'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/bintray/gradle-bintray-plugin.git'
    }
}
