//Apply the publishing plugin
buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.8"
        classpath "gradle.plugin.com.github.maiflai:gradle-scalatest:0.16"
    }
}

plugins {
    id 'scala'
    id 'idea'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.9.7'
    id "com.jfrog.bintray" version "1.7.3"
    id 'maven-publish'
}

apply plugin: "com.github.maiflai.scalatest"

group 'com.charlesahunt'
version = '1.1.4'

sourceCompatibility = 1.8
targetCompatibility = 1.8

apply plugin: 'scala'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'java-gradle-plugin'
apply plugin: 'com.jfrog.bintray'
apply plugin: "com.gradle.plugin-publish"

repositories {
    jcenter()
    mavenCentral()
}

task wrapper(type: Wrapper) {
    gradleVersion = "4.2"
}

dependencies {
    compile gradleApi()
    compile 'org.scala-lang:scala-library:2.12.3'
    compile 'org.scala-sbt:io_2.12:1.1.0'
    compile 'com.github.os72:protoc-jar:3.4.0'
    compile 'com.trueaccord.scalapb:compilerplugin_2.12:0.6.5'
    compile 'com.typesafe.scala-logging:scala-logging_2.12:3.7.2'
    testCompile group: 'org.scalatest', name: 'scalatest_2.12', version: "3.0.1"
    testRuntime 'org.pegdown:pegdown:1.4.2'
    zinc 'com.typesafe.zinc:zinc:0.3.13'
    compile 'com.google.protobuf:protobuf-java:3.4.0'
    compile 'com.trueaccord.scalapb:scalapb-runtime-grpc_2.12:0.6.6'
    compile 'com.trueaccord.scalapb:scalapb-runtime_2.12:0.6.6'

    testCompile gradleTestKit()
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

gradlePlugin {
    plugins {
        scalapbPlugin {
            id = 'com.charlesahunt.scalapb'
            implementationClass = 'com.charlesahunt.scalapb.ScalaPBPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/CharlesAHunt/scalapb-gradle-plugin'
    vcsUrl = 'git@github.com:CharlesAHunt/scalapb-gradle-plugin.git'
    tags = ['scala', 'scalapb', 'proto', 'protobuf']
    
    plugins {
        scalapbPlugin {
            id = 'com.charlesahunt.scalapb'
            displayName = 'Gradle ScalaPB Plugin'
            description = 'This plugin uses the [ScalaPB](http://scalapb.github.io) compiler to generate Scala case' +
                    ' classes from protocol buffers and put them under the managed sources directory in your project.'
        }
    }
}

// Plugin publication
publishing {
    publications {
        pluginPublication (MavenPublication) {
            from        components.java
            groupId     project.group
            artifactId  project.name
            version     project.version
        }
    }
}