plugins {
    id 'scala'
    id 'idea'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.9.9'
    id 'com.github.maiflai.scalatest' version '0.19'
    id 'com.jfrog.bintray' version '1.7.3'
    id 'maven-publish'
}

group 'com.charlesahunt'
version = '1.1.7'

sourceCompatibility = 1.8
targetCompatibility = 1.8


repositories {
    jcenter()
    mavenCentral()
}

task wrapper(type: Wrapper) {
    gradleVersion = "4.2"
}

dependencies {
    compile gradleApi()
    compile 'org.scala-lang:scala-library:2.12.5'
    compile 'org.scala-sbt:io_2.12:1.1.2'
    compile 'com.github.os72:protoc-jar:3.5.1.1'
    compile 'com.typesafe.scala-logging:scala-logging_2.12:3.7.2'
    compile 'com.google.protobuf:protobuf-java:3.5.1'
    compile 'com.trueaccord.scalapb:compilerplugin_2.12:0.6.7'
    compile 'com.trueaccord.scalapb:scalapb-runtime-grpc_2.12:0.6.7'
    compile 'com.trueaccord.scalapb:scalapb-runtime_2.12:0.6.7'

    testCompile group: 'org.scalatest', name: 'scalatest_2.12', version: "3.0.1"
    testCompile gradleTestKit()

    testRuntime 'org.pegdown:pegdown:1.4.2'
    zinc 'com.typesafe.zinc:zinc:0.3.15'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

pluginBundle {
    website = 'https://github.com/CharlesAHunt/scalapb-gradle-plugin'
    vcsUrl = 'git@github.com:CharlesAHunt/scalapb-gradle-plugin.git'
    tags = ['scala', 'scalapb', 'proto', 'protobuf']

    plugins {
        scalapbPlugin {
            id = 'com.charlesahunt.scalapb'
            displayName = 'Gradle ScalaPB Plugin'
            description = 'This plugin uses the [ScalaPB](http://scalapb.github.io) compiler to generate Scala case' +
                    ' classes from protocol buffers and put them under the managed sources directory in your project.'
        }
    }
}

// Plugin publication
publishing {
    publications {
        pluginPublication (MavenPublication) {
            from        components.java
            groupId     project.group
            artifactId  project.name
            version     project.version
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'com.charlesahunt.scalapb'
        name = 'gradle-scalapb'
        userOrg = 'com.charlesahunt'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/bintray/gradle-bintray-plugin.git'
    }
}
